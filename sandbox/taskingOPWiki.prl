! https://sourceforge.net/p/openpearl/wiki/Tutorial%20-%20Tasking/
/* producer and consumer share a fixed-size buffer used as a queue. 
The producer’s job is to generate data and put this in the buffer. 
The consumer’s job is to consume the data from this buffer, one at a time. 
Because the producer can possibly generate data quicker than the consumer can process a buffer is needed. */


The problem must be solved by synchronisation of the p roducer and
consumer task. Especially it must be assured, that the producer
doesn't try data in a full buffer and the consumer doesn't try to read
data from an empty buffer.

MODULE(ConsumerProducer);
SYSTEM;
  stdout: StdOut;

PROBLEM;
  SPC stdout DATION OUT SYSTEM ALPHIC GLOBAL;
  DCL termout DATION OUT ALPHIC DIM(*, 80) FORWARD STREAM CREATED(stdout);
  DCL semaTermout SEMA PRESET(1);
  DCL semaEmpty SEMA PRESET(1);
  DCL semaFull SEMA PRESET(0);
  DCL semaWait SEMA PRESET(1);

  DCL MAXBUFFER INV FIXED INIT(5);
  DCL buffer(1:MAXBUFFER) FIXED;
  DCL bufferIn FIXED INIT(1);
  DCL bufferOut FIXED INIT(1);  
  DCL counter FIXED INIT(0);

producer: TASK PRIO 5;
  TO 10 REPEAT
     REQUEST semaTermout;
     OPEN termout;
     PUT 'Producer bufferIn=', bufferIn, ': counter=', counter TO termout BY A, F(1), A, F(1), SKIP;
     CLOSE termout;
     RELEASE semaTermout;

     REQUEST semaEmpty;
     REQUEST semaWait;
     counter := counter + 1;
     buffer(bufferIn) := counter;

     bufferIn := bufferIn + 1;
     IF ( bufferIn > MAXBUFFER) THEN
    bufferIn := 1;
     FIN;

     RELEASE semaWait;
     RELEASE semaFull;
     AFTER 0.5 SEC RESUME;  
  END;
END;

consumer: TASK PRIO 5;
  DCL item FIXED INIT(0);

  TO 10 REPEAT
     REQUEST semaTermout;
     OPEN termout;
     PUT 'Consumer bufferOut=', bufferOut, ': counter=', counter TO termout BY A, F(1), A, F(1), SKIP;
     CLOSE termout;
     RELEASE semaTermout;

     REQUEST semaFull;
     REQUEST semaWait;
     item := buffer(bufferOut);
     bufferOut := bufferOut + 1;

     IF ( bufferOut > MAXBUFFER) THEN
    bufferOut := 1;
     FIN;

     RELEASE semaWait;
     RELEASE semaEmpty;
  END;
END;

main: TASK PRIO 1 MAIN;
   ACTIVATE producer;
   ACTIVATE consumer;   
END;

MODEND;

